apply plugin: 'maven-publish'
apply plugin: 'signing'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables, setup below.
                groupId project.group
                artifactId "android-device-names"
                version project.version

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                pom {
                    name = "android-device-names"
                    description = "A small Android library to get the market name of an Android device."
                    url = "https://github.com/antonio2904/AndroidDeviceNames"
                    licenses {
                        license {
                            name = 'Apache License'
                            url = 'https://github.com/jaredrummler/AndroidDeviceNames/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = "antonio2904"
                            name = "Antony Leons"
                            email = "antonyleons29@gmail.com"
                        }
                    }

                    // Version control info.
                    scm {
                        connection = "scm:git:github.com/antonio2904/AndroidDeviceNames.git"
                        developerConnection = "scm:git:ssh://github.com/antonio2904/AndroidDeviceNames.git"
                        url = "https://github.com/antonio2904/AndroidDeviceNames/tree/master"
                    }
                }
            }
        }
    }

    signing {
        // Support in-memory PGP signing
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        def signingKeyId = findProperty("signingKeyId")
        
        if (signingKey && signingPassword) {
            useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        }
        
        sign publishing.publications.release
    }
}
